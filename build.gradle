plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.4.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

def javaVersion = 21

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':lib')

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'org.liquibase:liquibase-core'
    implementation 'com.github.f4b6a3:ulid-creator:5.2.3'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'


    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
}

openApiGenerate {
    generatorName = 'spring'
    library = 'spring-boot'
    inputSpec = "$projectDir/src/main/resources/openapi.yaml"
    outputDir = "$buildDir/generated/openapi"
    apiPackage = 'com.example.backendchat.api'
    modelPackage = 'com.example.backendchat.api.model'
    invokerPackage = 'com.example.backendchat.api.invoker'
    configOptions = [
        useSpringBoot3: 'true',
        interfaceOnly : 'true',
        delegatePattern: 'true',
        useTags: 'true',
        openApiNullable: 'false'
    ]
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/openapi/src/main/java"
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
